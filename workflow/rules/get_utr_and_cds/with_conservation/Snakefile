#    FilTar: Integrating RNA-Seq data to improve microRNA target prediction accuracy in animals
#    Copyright (C) 2019 Thomas Bradley
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program.  If not, see <https://www.gnu.org/licenses/>


def get_bed_file_by_feature(wildcards):
    if wildcards.feature == "3UTR":
        if config['reannotation'] == True:
            return rules.get_extended_bed_file.output
        else:
            return rules.split_filtered_bed6.output
    elif wildcards.feature == "CDS":
        if config['reannotation'] == True:
            return rules.split_CDS.output
        else:
            return rules.split_filtered_bed6.output
    else:
        raise Exception(f"Unknown feature: {wildcards.feature}")


rule index_maf_files:
    input:
        maf="data/maf_{species}/chr{chrom}.maf",
    output:
        protected("data/{species}_chr{chrom}.mafindex"),
    conda:
        "../../../envs/biopython.yaml"
    script:
        "../../../scripts/biopython_maf_processing.py"


rule biopython_get_msa:
    input:
        maf="data/maf_{species}/chr{chrom}.maf",
        bed=get_bed_file_by_feature,
        maf_index=rules.index_maf_files.output,
        shell_script="workflow/scripts/targetscan7/convert_fasta_to_tsv.sh",
    output:
        temp("results/msa/{species}_{tissue_tx_model}_chr{chrom}_{feature}_msa.tsv"),
    conda:
        "../../../envs/biopython.yaml"
    script:
        "../../../scripts/biopython_maf_processing2.py"
