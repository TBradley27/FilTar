#    FilTar: Integrating RNA-Seq data to improve microRNA target prediction accuracy in animals
#    Copyright (C) 2019 Thomas Bradley
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program.  If not, see <https://www.gnu.org/licenses/>

def get_gtf_file(wildcards):
        sci_species_name = config['sci_species_name'][wildcards.species]
        genome_build = config['genome_build'][wildcards.species]

        return ( 'data/' + sci_species_name + '.' + genome_build + '.{}.mod.chr.gtf'.format(config['ensembl_release'])  )

def get_bed_file(wildcards):
        sci_species_name = config['sci_species_name'][wildcards.species]
        genome_build = config['genome_build'][wildcards.species]

        return('results/' + sci_species_name + '.' + genome_build + '_chr{}.{}.filtered.bed'.format(wildcards.chrom, config['ensembl_release']))

def get_bed6_file(wildcards):
        sci_species_name = config['sci_species_name'][wildcards.species]
        genome_build = config['genome_build'][wildcards.species]

        return ( 'results/' + sci_species_name + '.' + genome_build + '.{}.chr{}.filtered.bed6'.format(config['ensembl_release'], wildcards.chrom) )

rule get_bed6_file: #three_prime_utrs only
        input:
            script="scripts/gtf_to_bed.sh",
            gtf="data/{genus_species}.{build}.{ensembl_release}.chr.gtf"
        output:
            "results/{genus_species}.{build}.{ensembl_release}.bed6"
        shell:
            "{input.script} {input.gtf} {output}"

rule filter_bed6_file:
        input:  'results/{genus_species}.{build}.{ensembl_release}.bed6'
        output:  'results/{genus_species}.{build}.{ensembl_release}.filtered.bed6'
        script: 'filter_bed6.R'

rule split_bed6_file:
        input: 'results/{genus_species}.{build}.{ensembl_release}.filtered.bed6'
        output: 'results/{genus_species}.{build}.{ensembl_release}.chr{chrom}.filtered.bed6'
        shell: 'grep -E "^{wildcards.chrom}\s" {input} > {output} || true'

# derived from https://gist.github.com/gireeshkbogu/f478ad8495dca56545746cd391615b93

rule convert_gtf_to_genepred:
     input:
         gtf="data/{species_prefix}.{ensembl_release}.mod.chr.gtf"
     output:
         "results/{species_prefix}.{ensembl_release}.genePred"
     conda: "envs/ucsc.yaml"
     shell:
         "gtfToGenePred {input.gtf} {output}"

rule convert_genepred_to_bed12:
     input:
         genepred="results/{species_prefix}.{ensembl_release}.genePred"
     output:
         "results/{species_prefix}.{ensembl_release}.bed"
     wildcard_constraints:
               species_prefix="((?!chr).)*" # anything not containing chr
     conda: "envs/ucsc.yaml"
     shell:
         "genePredToBed {input.genepred} {output}"

rule get_bedgraph:
    input:
        sorted_bam="results/bam/run/{species}/{accession}.bam.sorted",
    output:
        "results/bam/run/{species}/{accession}.bedgraph"
    conda:
        "envs/bedtools.yaml"
    wildcard_constraints:
               accession="((?!_chr).)*"
    priority: 10
    shell:
        "genomeCoverageBed -bg -ibam {input.sorted_bam} -split > {output}"

rule split_bedgraph:
        input: 'results/bam/run/{species}/{accession}.bedgraph'
        output: 'results/bam/run/{species}/{accession}_chr{chrom}.bedgraph'
        shell: 'grep -E "^{wildcards.chrom}\s" {input} > {output}'

rule merge_bedgraphs_by_run:
        input:
             lambda wildcards: expand("results/bam/run/{species}/{accession}_chr{chrom}.bedgraph", species=wildcards.species, accession=config['samples'][wildcards.sample], chrom=wildcards.chrom)
        output:
             temp("results/bam/sample/{species}/{sample}_chr{chrom}_tmp.bedgraph")
        wildcard_constraints:
             sample="((?!tmp).)*"
        conda:
             "envs/bedtools.yaml"
        script: "merge_bedgraphs.py"

rule avg_merged_bedgraph_by_run:
        input:
                "results/bam/sample/{species}/{sample}_chr{chrom}_tmp.bedgraph"
        output:
                temp("results/bam/sample/{species}/{sample}_chr{chrom}.bedgraph")
        wildcard_constraints:
             sample="((?!tmp).)*"
        script:
                "get_average_bedgraph.R"

rule merge_bedgraphs_by_sample:
        input:
             lambda wildcards: expand("results/bam/sample/{species}/{sample}_chr{chrom}.bedgraph", species=wildcards.species, sample=config['tissues'][wildcards.species][wildcards.cell_line], chrom=wildcards.chrom)
        output:
             temp("results/bam/tissue/{species}/{cell_line}_chr{chrom}.bedgraph.tmp")
        wildcard_constraints:
               cell_line="((?!chr).)*" 
        conda:
             "envs/bedtools.yaml"
        script: "merge_bedgraphs.py"

rule avg_merged_bedgraph_by_sample:
        input:
                "results/bam/tissue/{species}/{cell_line}_chr{chrom}.bedgraph.tmp"
        output:
                temp("results/bam/tissue/{species}/{cell_line}_chr{chrom}.bedgraph")
        wildcard_constraints:
               cell_line="((?!chr).)*" 
        script:
                "get_average_bedgraph.R"

rule split_bed12:
        input: 'results/{species_prefix}.{ensembl_release}.bed'
        output:'results/{species_prefix}_chr{chrom}.{ensembl_release}.bed'
        shell: 'grep -E "^{wildcards.chrom}\s" {input} > {output}'

rule filter_bed12:
        input: 'results/{species_prefix}_chr{chrom}.{ensembl_release}.bed'
        output: 'results/{species_prefix}_chr{chrom}.{ensembl_release}.filtered.bed'
        script: "filter_bed12.R"

rule reannotate_3utrs:
    input:
       script="scripts/identifyDistal3UTR.pl",
       bed=get_bed_file,
       bedgraphs= 'results/bam/tissue/{species}/{cell_line}_chr{chrom}.bedgraph'
    output:
       "results/bed/{species}_{cell_line}_chr{chrom}.utr.bed"
    params:
        percentage_cutoff=0.80,
        coverage_cutoff=0.05,
        window_size=100
    shell:
       "{input.script} -i {input.bedgraphs} -p {params.percentage_cutoff} -c {params.coverage_cutoff} -w {params.window_size} -m {input.bed} -o {output}"

rule get_extended_bed_file:
         input:
            normal_bed= get_bed6_file,
            extended_bed="results/bed/{species}_{tissue}_chr{chrom}.utr.bed",
            all_transcripts="results/bed/{species}_chr{chrom}_all_transcripts.txt",
            tx_quant="results/salmon/{species}/{tissue}.sf"
         output:
            "results/bed/{species}_{tissue}_3UTR.chr{chrom}.bed"
         script:
             "extend_bed2.R"

rule aggregrate_extended_bed_files:
        input: lambda wildcards: expand("results/bed/{species}_{tissue}_3UTR.chr{chrom}.bed", chrom=config['chromosomes'][wildcards.species], species=wildcards.species, tissue=wildcards.tissue)
        output: "results/bed/{species}_{tissue}_3UTR.bed"
        shell: "cat {input} > {output}"

### Generate AIR file

rule identify_APA_sites:
     input:
        script="scripts/predictAPA.pl",
        bedgraphs= "results/bam/tissue/{species}/{tissue}_chr{chrom}.bedgraph",
        bed="results/bed/{species}_{tissue}_chr{chrom}.utr.bed"
     output:
        "results/targets/{species}_{tissue}_chr{chrom}.APA.txt"
     shell:
        "{input.script} -i {input.bedgraphs} -g 1 -n 1 -u {input.bed}  -o {output}"

rule aggregate_APA_sites:
        input: lambda wildcards: expand("results/targets/{species}_{tissue}_chr{chrom}.APA.txt", chrom=config['chromosomes'][wildcards.species], species=wildcards.species, tissue=wildcards.tissue)
        output: protected("results/targets/{species}_{tissue}.APA.txt")
        shell: "cat {input} | sed '1b;/Gene/d' > {output}"

rule get_utr_lengths_chr:
	input: 'results/bed/{species}_{tissue}_3UTR.chr{chrom}.bed'
	output: 'results/utrs/{species}_{tissue}_chr{chrom}.utr.lengths.tsv'
	script: 'get_utr_lengths.R'

rule get_utr_lengths:
        input:  'results/bed/{species}_{tissue}_3UTR.bed'
        output: 'results/utrs/{species}_{tissue}.utr.lengths.tsv'
        script: 'get_utr_lengths.R'

rule get_AIR_file:
        input: "results/targets/{species}_{tissue}_chr{chrom}.APA.txt", "results/utrs/{species}_{tissue}_chr{chrom}.utr.lengths.tsv" #output from APA usage file
        output: "results/targets/{species}_{tissue}_chr{chrom}.AIRs.txt"
        script: "get_tissue_specific_APA_file.R"

### Get CDS bed file

rule gtf_to_bed_CDS:
        input:
            gtf=get_gtf_file,
            script="scripts/pre-processing/gtf_to_bed.sh"
        output:
            "results/bed/{species}_{tissue}_CDS.tmp.bed"
        shell:
            "{input.script} {input.gtf} CDS {output}"

rule filter_CDS:
	input: "results/bed/{species}_{tissue}_CDS.tmp.bed"
	output: "results/bed/{species}_{tissue}_CDS.filtered.bed"
	script: "filter_bed6.R"

rule split_CDS:
        input: "results/bed/{species}_{tissue}_CDS.filtered.bed"
        output: "results/bed/{species}_{tissue}_CDS.chr{chrom}.bed"
        shell: 'grep -E "^{wildcards.chrom}\s" {input} > {output} || true'

### filtering by expression level

rule expression_filter_contextpp_scores_chr:
        input:
                contextpp_scores="results/targets/{species}_{tissue}_chr{chrom}_msa.contextpp.tmp.tsv",
                expression_values="results/salmon/{species}/{tissue}.sf"
        params:
                tpm_expression_threshold=config['TPM_expression_threshold']
        output: protected("results/targets/{species}/{tissue}_chr{chrom}.contextpp.tsv")
        script: 'filter_contextpp_scores.R'

rule expression_filter_contextpp_scores:
        input:
            contextpp_scores="results/targets/{species}/{tissue}.contextpp.tmp.tsv",
            expression_values="results/salmon/{species}/{tissue}.sf"
        params:
            tpm_expression_threshold=config['TPM_expression_threshold']
        output: protected("results/targets/{species}/{tissue}.contextpp.tsv")
        script: 'filter_contextpp_scores.R'

rule expression_filter_miRanda_chr:
        input:
            miRanda_scores="results/targets/miRanda/{species}/{tissue}_chr{chrom}.temp.tsv",
            expression_values="results/salmon/{species}/{tissue}.sf"
        params:
            tpm_expression_threshold= config['TPM_expression_threshold']
        output: protected("results/targets/miRanda/{species}/{tissue}/chr{chrom}.tsv")
        script: 'filter_miRanda_scores.R'

rule expression_filter_miRanda:
        input:
            miRanda_scores="results/targets/miRanda/{species}/{tissue}.temp.tsv",
            expression_values="results/salmon/{species}/{tissue}.sf"
        params:
            tpm_expression_threshold=config['TPM_expression_threshold']
        wildcard_constraints:
               tissue="((?!temp).)*"
        output: protected("results/targets/miRanda/{species}/{tissue}.tsv")
        script: 'filter_miRanda_scores.R'
