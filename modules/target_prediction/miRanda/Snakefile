#    FilTar: Integrating RNA-Seq data to improve microRNA target prediction accuracy in animals
#    Copyright (C) 2019 Thomas Bradley
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program.  If not, see <https://www.gnu.org/licenses/>


def get_dna_file(wildcards):
    sci_species_name = config["sci_species_name"][wildcards.species]
    genome_build = config["genome_build"][wildcards.species]

    return (
        "data/"
        + sci_species_name
        + "."
        + genome_build
        + ".dna.chromosome.{}.fa".format(wildcards.chrom)
    )


def get_miRanda_strict_bool(wildcards):
    if config["miRanda.strict"] == True:
        return " -strict"
    elif config["miRanda.strict"] == False:
        return ""
    else:
        raise Exception(
            "Value for 'miRanda.strict' config option must be 'True' or 'False'"
        )


rule fix_fasta_output:
    input:
        "results/msa/{species}_{tissue}_chr{chrom}_3UTR.tmp2.fa",
    output:
        temp("results/msa/{species}_{tissue}_chr{chrom}.fa"),
    shell:
        "sed 's/(+)//g' {input} | sed 's/(-)//g' > {output}"


rule miRanda:
    input:
        utr="results/msa/{species}_{tissue}_chr{chrom}.fa",
        mirna="data/mirbase_mature_{species}.filtered.fa",
    output:
        temp("results/targets/miRanda/{species}/{tissue}_chr{chrom}.txt"),
    conda:
        "envs/miRanda.yaml"
    params:
        get_miRanda_strict_bool,
    shell:
        "miranda {input.mirna} {input.utr} {params} -sc {config[miRanda.minimum_alignment_score]} -en {config[miRanda.minimum_energy_score]} -scale {config[miRanda.5_prime_3_prime_scaling_factor]} -go {config[miRanda.alignment_gap_open_penalty]} -ge {config[miRanda.alignment_gap_extension_penalty]} > {output}"


rule convert_miRanda_to_tsv:
    input:
        "results/targets/miRanda/{species}/{tissue}_chr{chrom}.txt",
    output:
        temp("results/targets/miRanda/{species}/{tissue}_chr{chrom}.temp.tsv"),
    shell:
        "scripts/convert_miRanda_to_tsv.sh {input} {output}"


rule aggregate_miRanda_output_no_reannotation:
    input:
        lambda wildcards: expand(
            "results/targets/miRanda/{species}/{tissue}_chr{chrom}.temp.tsv",
            chrom=config["chromosomes"][wildcards.species],
            species=wildcards.species,
            tissue=wildcards.tissue,
        ),
    output:
        temp("results/targets/miRanda/{species}/{tissue}.temp.tsv"),
    wildcard_constraints:
        tissue="((?!_chr).)*",
    shell:
        "cat {input} > {output}"


rule add_miRanda_header:
    input:
        "results/targets/miRanda/{species}/{tissue}.temp.tsv",
    output:
        temp("results/targets/miRanda/{species}/{tissue}.temp2.tsv"),
    script:
        "add_miRanda_header.R"
